<!DOCTYPE html>

<html ng-app>
  <head>
    <meta charset="utf-8">
    <title>Portal</title>
    <link rel="stylesheet" href="portal.css">
  </head>
  <body gaze>
    <!--p id="text">{{ctrl.gaze.data}}</p-->
    <!--p id="text">{{ctrl.distance}} {{ctrl.mutualGaze}} {{ctrl.mutualGazeDuration}}</p-->
    <canvas id='a' width='400' height='400'></canvas>        
    <script type='x-shader/x-fragment' id='fragmentshader'>
      uniform float cameraZ;
      uniform float time;

      varying float vAlpha;
      varying float vDistance;
      varying vec3 vColor;
      varying vec3 vShift;

      float rand(vec2 co){
        return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
      }

      void main() {
        vec4 color = vec4(vColor, 1.0);
        vec4 shift = vec4(vShift, 1.0);
        vec4 mixed = mix(color, shift, 0.9);
        vec4 amixed = vec4(mixed.xyz, vAlpha);
        gl_FragColor = amixed;
      }
    </script>

    <script type='x-shader/x-vertex' id='vertexshader'>
      uniform float zoomSize;
      uniform float scale;
      uniform float cameraZ;
      uniform float time;
      uniform float alpha;
      uniform vec3 shift;

      attribute float distance;
      attribute vec3 color;
    
      varying float vAlpha;
      varying float vDistance;
      varying vec3 vColor;
      varying vec3 vShift;

      //
      // Description : Array and textureless GLSL 2D simplex noise function.
      //      Author : Ian McEwan, Ashima Arts.
      //  Maintainer : ijm
      //     Lastmod : 20110822 (ijm)
      //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
      //               Distributed under the MIT License. See LICENSE file.
      //               https://github.com/ashima/webgl-noise
      //

      vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec2 mod289(vec2 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec3 permute(vec3 x) {
        return mod289(((x*34.0)+1.0)*x);
      }

      float snoise(vec2 v)
      {
        const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                            0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                           -0.577350269189626,  // -1.0 + 2.0 * C.x
                            0.024390243902439); // 1.0 / 41.0
      // First corner
        vec2 i  = floor(v + dot(v, C.yy) );
        vec2 x0 = v -   i + dot(i, C.xx);

      // Other corners
        vec2 i1;
        //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
        //i1.y = 1.0 - i1.x;
        i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
        // x0 = x0 - 0.0 + 0.0 * C.xx ;
        // x1 = x0 - i1 + 1.0 * C.xx ;
        // x2 = x0 - 1.0 + 2.0 * C.xx ;
        vec4 x12 = x0.xyxy + C.xxzz;
        x12.xy -= i1;

      // Permutations
        i = mod289(i); // Avoid truncation effects in permutation
        vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
          + i.x + vec3(0.0, i1.x, 1.0 ));

        vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
        m = m*m ;
        m = m*m ;

      // Gradients: 41 points uniformly over a line, mapped onto a diamond.
      // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

        vec3 x = 2.0 * fract(p * C.www) - 1.0;
        vec3 h = abs(x) - 0.5;
        vec3 ox = floor(x + 0.5);
        vec3 a0 = x - ox;

      // Normalise gradients implicitly by scaling m
      // Approximation of: m *= inversesqrt( a0*a0 + h*h );
        m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

      // Compute final noise value at P
        vec3 g;
        g.x  = a0.x  * x0.x  + h.x  * x0.y;
        g.yz = a0.yz * x12.xz + h.yz * x12.yw;
        return 130.0 * dot(m, g);
      }

      void main() {
        vDistance = distance;
        vColor = color;
        vAlpha = alpha;
        vShift = shift;

        vec3 perturbation = vec3(snoise( vec2(position.x, time * 0.001)),
                    snoise(vec2(position.y, time * 0.001)),
                    snoise(vec2(position.z, time * 0.001)));
        vec4 mvPosition = modelViewMatrix * vec4(position + 400.0 * perturbation, 1.0 );


        gl_PointSize = scale * 10.0 * zoomSize / length(mvPosition.xyz);
        gl_Position = projectionMatrix * mvPosition;
      }
    </script>
    
    <!--script src="packages/shadow_dom/shadow_dom.min.js"></script-->
    <script type="application/dart" src="portal.dart"></script>
    <script src="packages/browser/dart.js"></script>
  </body>
</html>